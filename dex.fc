slice clone_slice(slice s) {
  return begin_cell().store_slice(s).end_cell().begin_parse();
}

slice pack_addr(int wc, slice addr) {
  return begin_cell().store_int(wc, 8).store_slice(addr).end_cell().begin_parse();
}

(slice, slice) ~load_currency(slice cs) {
  int currency_type = cs~load_uint(8);
  if (currency_type) {
    slice currency = cs~load_bits(264);
    return (cs, currency);
  } else {
    slice currency = cs~load_bits(32);
    return (cs, currency);
  }
}

builder store_currency(builder b, slice currency) {
  int currency_type = currency.slice_bits() != 32;
  return b.store_uint(currency_type ? 1 : 0, 8).store_slice(currency);
}

(slice, int, slice, int, slice, int, int) parse_order(slice cs) {
  slice ref = cs~load_ref().begin_parse();

  return (cs~load_bits(264), cs~load_grams(), cs~load_currency(), ref~load_grams(), ref~load_currency(), ref~load_int(1), ref~load_uint(32));
}

builder store_order(sender, from_value, from_currency, to_value, to_currency, paid) {
  int time = now();
;;  int time = 0; ;; todo: for test

  builder ref = begin_cell()
    .store_grams(to_value)
    .store_currency(to_currency)
    .store_int(paid, 1) ;; paid
    .store_uint(time + 86400, 32); ;; lifetime 1 day

  builder b = begin_cell()
    .store_ref(ref.end_cell())
    .store_slice(sender)
    .store_grams(from_value)
    .store_currency(from_currency);

  return b;
}

int is_gram(slice currency) {
  return (currency.slice_bits() == 32) & (currency~load_int(32) == 0);
}

int get_extra_currency(slice currency) {
  if (currency.slice_bits() != 32) {
    return 0;
  }
  return currency~load_int(32);
}

() send_message_back(int action, slice sender, int value, int mode, int query_id, int query_action) impure {
  ;; int_msg_info ihr_disabled:1 bounce:1 bounced:0 src:MsgAddress -> 011000
  var msg = begin_cell()
      .store_uint(0x18, 6)
      .store_uint(0x4, 3)
      .store_slice(sender)
      .store_grams(value)
      .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
      .store_uint(action, 32)
      .store_uint(query_id, 64)
      .store_uint(query_action, 32);

  send_raw_message(msg.end_cell(), mode);
}

() transfer(slice addr, int value, slice currency, int order_id) impure {
  int isgram = is_gram(currency);
  int currency_id = get_extra_currency(currency);

  if (isgram) {

    builder msg = begin_cell()
      .store_uint(0x18, 6)
      .store_uint(0x4, 3)
      .store_slice(addr)
      .store_grams(value)
      .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1);

    send_raw_message(msg.end_cell(), 0);

  } elseif (currency_id) {
    int gas_grams = 1 << 30; ;; ~1 gram of value to process

    cell currencies = new_dict();
    currencies~idict_set_builder(32, currency_id, begin_cell().store_grams(value));

    builder msg = begin_cell()
      .store_uint(0x18, 6)
      .store_uint(0x4, 3)
      .store_slice(addr)
      .store_grams(gas_grams)
      .store_dict(currencies)
      .store_uint(0, 4 + 4 + 64 + 32 + 1 + 1);

    send_raw_message(msg.end_cell(), 0);

  } else {
    int gas_grams = 1 << 30; ;; ~1 gram of value to process and obtain answer

    builder msg = begin_cell()
      .store_uint(0x18, 6)
      .store_uint(0x4, 3)
      .store_slice(currency)
      .store_grams(gas_grams)
      .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
      .store_uint(1, 32) ;; transfer
      .store_uint(order_id, 64)
      .store_slice(addr)
      .store_grams(value);

    send_raw_message(msg.end_cell(), 0);

  }
}

() transfer_from(slice addr, int value, slice currency, int order_id) impure {
  int gas_grams = 1 << 30; ;; ~1 gram of value to process and obtain answer

  (int my_wc, slice my_val) = parse_var_addr(my_address());
  slice my_addr = pack_addr(my_wc, my_val);
;;  slice my_addr = addr; ;; todo: for test

  builder ref = begin_cell()
        .store_slice(addr)
        .store_slice(my_addr)
        .store_grams(value);

  builder msg = begin_cell()
      .store_uint(0x18, 6)
      .store_uint(0x4, 3)
      .store_slice(currency)
      .store_grams(gas_grams)
      .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
      .store_uint(2, 32) ;; transfer from
      .store_uint(order_id, 64) ;; transfer from
      .store_ref(ref.end_cell());

  send_raw_message(msg.end_cell(), 0);
}

(cell, int, slice) find_match_order(cell orders, int _from_value, slice _from_currency, int _to_value, slice _to_currency) {
  int result_id = 0;
  slice result_sender = null();
  int time = now();
;;  int time = 0; ;; todo: for test

  int i = -1;
  do {
    (i, slice order_slice, int f) = orders.udict_get_next?(64, i);
    if (f) {
      (slice sender, int from_value, slice from_currency, int to_value, slice to_currency, int paid, int expired_at) = parse_order(order_slice);

      if (paid & (_from_value == to_value) & (_from_currency.slice_hash() == to_currency.slice_hash()) &
          (_to_value == from_value) & (_to_currency.slice_hash() == from_currency.slice_hash())) {
        result_id = i;
        result_sender = sender;
      } elseif (expired_at < time) {
        if (paid) {
          transfer(sender, from_value, from_currency, i); ;; return funds
        }

        orders~udict_delete?(64, i);
      }
    }
  } until ((~ f) | (result_id > 0));

  return (orders, result_id, result_sender);
}

(cell, int) try_match_order(cell orders, slice sender, int id, int from_value, slice from_currency, int to_value, slice to_currency) impure {
  (orders, int match_order_id, slice match_order_sender) = find_match_order(orders, from_value, from_currency, to_value, to_currency);

  if (match_order_id > 0) {
    transfer(sender, to_value, to_currency, id);
    transfer(match_order_sender, from_value, from_currency, match_order_id);

    if (id > 0) {
      orders~udict_delete?(64, id);
    }
    orders~udict_delete?(64, match_order_id);
  }

  return (orders, match_order_id);
}

() process_user_msg(int action, int msg_value, cell extra_currencies, slice sender, slice cs) impure {
  int query_id = cs~load_uint(64);

  if (action > 2) {
    if (action < 0x80000000) { ;; not response
      send_message_back(0xffffffff, sender, 0, 64, query_id, action);
    }
    return ();
  }

  slice ds = get_data().begin_parse();
  (int seqno, cell orders) = (ds~load_uint(64), ds~load_dict());
  ds.end_parse();
  int new_seqno = seqno;
  int need_return_one_gram = 0;

  if (action == 1) { ;; insert order
    int from_value = cs~load_grams();
    slice from_currency = cs~load_currency();
    int to_value = cs~load_grams();
    slice to_currency = cs~load_currency();

    int matched = 0;
    int isgram = is_gram(from_currency);
    if (isgram) {
      msg_value -= 1000000000;
      need_return_one_gram = 1;
    }
    int currency_id = get_extra_currency(from_currency);
    if (currency_id) {
        (slice currency_slice, _) = extra_currencies.idict_get?(32, currency_id);
        msg_value = currency_slice~load_grams();
        need_return_one_gram = 0;
        isgram = -1;
    }

    if (isgram) {
      throw_unless(100, msg_value >= from_value);
      (orders, matched) = try_match_order(orders, sender, 0, from_value, from_currency, to_value, to_currency);
    } else {
      transfer_from(sender, from_value, from_currency, seqno + 1);
    }

    if (matched == 0) {
      new_seqno = seqno + 1;

      builder b = store_order(sender, from_value, from_currency, to_value, to_currency, isgram);
      orders~udict_set_builder(64, new_seqno, b);
    }

  } elseif (action == 2) { ;; cancel order

    int order_id = cs~load_uint(64);
    (slice order_slice, int order_found) = orders.udict_get?(64, order_id);
    if (order_found) {
      (slice order_sender, int from_value, slice from_currency, int to_value, slice to_currency, int paid, int expired_at) = parse_order(order_slice);

      if (order_sender.slice_hash() == sender.slice_hash()) {
        if (paid) {
          transfer(order_sender, from_value, from_currency, order_id); ;; return funds
        }

        orders~udict_delete?(64, order_id);
      }
    }

  }

  set_data(
    begin_cell()
      .store_uint(new_seqno, 64)
      .store_dict(orders)
      .end_cell()
  );

  send_message_back(0x90000000, sender, need_return_one_gram ? 1000000000 : 0, need_return_one_gram ? 2 : 64, query_id, action);
}

() process_trc20_reply(slice sender, slice cs, int ok) impure {
  int query_id = cs~load_uint(64);

  if (ok & (cs~load_uint(32) != 2)) { ;; query_action != transfer_from
    return ();
  }

  slice ds = get_data().begin_parse();
  (int seqno, cell orders) = (ds~load_uint(64), ds~load_dict());
  ds.end_parse();

  (slice order_slice, int order_found) = orders.udict_get?(64, query_id);
  if (order_found) {
    (slice order_sender, int from_value, slice from_currency, int to_value, slice to_currency, int paid, int expired_at) = parse_order(order_slice);

    if ((paid == 0) & (from_currency.slice_hash() == sender.slice_hash())) {

      if (ok) {

        (orders, int matched) = try_match_order(orders, order_sender, query_id, from_value, from_currency, to_value, to_currency);
        if (matched == 0) {
          builder b = store_order(order_sender, from_value, from_currency, to_value, to_currency, -1);

          orders~udict_set_builder(64, query_id, b);
        }
      } else {
        orders~udict_delete?(64, query_id);
      }
    }
  }

  set_data(
    begin_cell()
      .store_uint(seqno, 64)
      .store_dict(orders)
      .end_cell()
  );
}

() recv_internal(int msg_value, cell in_msg_cell, slice in_msg) impure {
  slice cs = in_msg_cell.begin_parse();
  int flags = cs~load_uint(4);
  slice sender_raw = cs~load_msg_addr();
  (int sender_wc, slice sender_addr) = parse_var_addr(sender_raw);
  slice sender = pack_addr(sender_wc, sender_addr);
  cs~load_grams(); ;; msg_value
  cell extra_currencies = cs~load_dict();

  cs = in_msg;
  int action = cs~load_uint(32);

  if ((action == 0) & (cs.slice_bits() == 0)) { ;; just accept grams
    return ();
  }

  if (flags & 1) { ;; bounced

    if (action == 2) { ;; error reply from TRC20 transfer_from
      process_trc20_reply(sender, cs, 0);
    }

  } elseif (action == 0x80000000) { ;; ok reply from TRC20 transfer_from

    process_trc20_reply(sender, cs, 1);

  } else {
    (action, cs) = parse_msg(action, cs);
    process_user_msg(action, msg_value, extra_currencies, sender, cs);
  }
}

() recv_external(slice sender, slice in_msg) impure {
  accept_message();
}

int seqno() method_id {
  slice ds = get_data().begin_parse();
  (int seqno, cell orders) = (ds~load_uint(64), ds~load_dict());
  ds.end_parse();

  return seqno;
}

;; currency - 32bit for extra currency or 264 bit for TRC20 tokens
_ orders(slice _sender, slice _from_currency, slice _to_currency) method_id {
  slice ds = get_data().begin_parse();
  (int seqno, cell orders) = (ds~load_uint(64), ds~load_dict());
  ds.end_parse();

  var result = nil;
  int i = -1;
  do {
    (i, slice order_slice, int f) = orders.udict_get_next?(64, i);
    if (f) {
      (slice sender, int from_value, slice from_currency, int to_value, slice to_currency, int paid, int expired_at) = parse_order(order_slice);

      int fit_sender = _sender.slice_empty?() | (_sender.slice_hash() == sender.slice_hash());
      int fit_from_currency = _from_currency.slice_empty?() | (_from_currency.slice_hash() == from_currency.slice_hash());
      int fit_to_currency = _to_currency.slice_empty?() | (_to_currency.slice_hash() == to_currency.slice_hash());

      if (fit_sender & fit_from_currency & fit_to_currency) {
        result = cons(triple(i, paid, triple(clone_slice(sender), pair(from_value, clone_slice(from_currency)), pair(to_value, clone_slice(to_currency)))), result);
      }
    }
  } until (~ f);

  return result;
}

_ my_orders(int sender_wc, int sender_addr) method_id {
  slice sender = begin_cell().store_int(sender_wc, 8).store_uint(sender_addr, 256).end_cell().begin_parse();
  slice empty = begin_cell().end_cell().begin_parse();
  return orders(sender, empty, empty);
}

;; TEST

int test_recv_internal(int value, cell in_msg_cell, slice in_msg) method_id {
  recv_internal(value, in_msg_cell, in_msg);
  return 0;
}